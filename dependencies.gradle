def VERSIONS = [
        'ch.qos.logback:logback-classic:1.2.+',
        'colt:colt:1.2.0',
        'com.amazonaws:aws-java-sdk-cloudwatch:1.12.268',
        'com.dynatrace.metric.util:dynatrace-metric-utils-java:1.5.0',
        'com.fasterxml.jackson.core:jackson-databind:2.13.4.2',
        'com.github.ben-manes.caffeine:caffeine:2.+',
        'com.github.charithe:kafka-junit:4.2.2',
        'com.github.tomakehurst:wiremock-jre8-standalone:2.33.2',
        'com.google.cloud:google-cloud-monitoring:3.3.4',
        'com.google.dagger:dagger:2.11',
        'com.google.dagger:dagger-compiler:2.11',
        'com.google.guava:guava:latest.release',
        'com.google.inject:guice:4.1.0',
        'com.hazelcast:hazelcast:5.1.5',
        'com.h2database:h2:latest.release',
        'com.microsoft.azure:applicationinsights-core:2.6.4',
        // metrics are better with https://github.com/Netflix/Hystrix/pull/1568 introduced
        // in hystrix 1.5.12, but Netflix re-released 1.5.11 as 1.5.18 late in 2018.
        // <=1.5.11 or 1.5.18 doesn't break with Micrometer, but open metrics won't be correct necessarily.
        'com.netflix.hystrix:hystrix-core:1.5.12',
        'com.netflix.spectator:spectator-reg-atlas:1.3.5',
        'com.newrelic.agent.java:newrelic-api:5.+',
        'com.signalfx.public:signalfx-java:1.0.20',
        'com.squareup.okhttp3:okhttp:4.10.0',
        'com.tngtech.archunit:archunit-junit5:0.23.1',
        'com.wavefront:wavefront-sdk-java:3.0.2',
        // 3.1.3 falis on Mac OS, 3.0.0 is the last release before that version
        // https://github.com/flapdoodle-oss/de.flapdoodle.embed.mongo/issues/352
        'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.0.0',
        'io.dropwizard.metrics:metrics-core:4.0.+',
        'io.dropwizard.metrics:metrics-graphite:4.1.+',
        'io.dropwizard.metrics:metrics-jmx:4.0.+',
        // all io.grpc libraries must use the same version
        'io.grpc:grpc-api:1.48.0',
        'io.grpc:grpc-core:1.48.0',
        'io.grpc:grpc-services:1.48.0',
        'io.grpc:grpc-stubs:1.48.0',
        'io.grpc:grpc-alts:1.48.0',
        'info.ganglia.gmetric4j:gmetric4j:latest.release',
        'io.prometheus:simpleclient_common:0.15.0',
        'io.prometheus:simpleclient_pushgateway:0.15.0',
        'javax.cache:cache-api:1.1.1',
        'javax.inject:javax.inject:1',
        'javax.xml.bind:jaxb-api:2.3.+',
        'net.sf.ehcache:ehcache:latest.release',
        'org.apache.httpcomponents:httpasyncclient:4.1.5',
        'org.apache.httpcomponents:httpclient::4.5.13',
        'org.apache.kafka:kafka-clients:2.+',
        'org.apache.kafka:kafka-streams:2.+',
        'org.apache.logging.log4j:log4j-core:2.+',
        'org.apache.tomcat.embed:tomcat-embed-core:8.+',
        'org.aspectj:aspectjweaver:1.8.+',
        'org.assertj:assertj-core:latest.release',
        'org.awaitility:awaitility:latest.release',
        'org.eclipse.jetty:jetty-client:9.+',
        'org.eclipse.jetty:jetty-server:9.+',
        'org.ehcache:ehcache:latest.release',
        'org.glassfish.jersey.core:jersey-server:2.+',
        'org.glassfish.jersey.inject:jersey-hk2:2.+',
        'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-inmemory:2.+',
        'org.hdrhistogram:HdrHistogram:latest.release',
        'org.hibernate:hibernate-entitymanager:5.+',
        'org.hsqldb:hsqldb:2.5.+', // 2.6.0 requires JDK 11.
        'org.jooq:jooq:3.14.+',
        'org.jsr107.ri:cache-ri-impl:1.0.0',
        'org.junit.jupiter:junit-jupiter:5.8.+',
        'org.junit.platform:junit-platform-launcher:1.8.+',
        'org.latencyutils:LatencyUtils:latest.release',
        'org.mockito:mockito-core:latest.release',
        'org.mockito:mockito-inline:latest.release',
        'org.mongodb:mongodb-driver-sync:latest.release',
        'org.slf4j:slf4j-api:1.7.+',
        'org.springframework:spring-context:5.3.9',
        'org.testcontainers:junit-jupiter:1.17.3',
        'org.testcontainers:kafka:1.17.3',
        'org.testcontainers:testcontainers:1.17.3',
        'ru.lanwen.wiremock:wiremock-junit5:latest.release',
        'software.amazon.awssdk:cloudwatch:2.18.28'
]

subprojects {
  plugins.withId('java-library') {
    dependencies {
      constraints {
        // Direct dependencies
        VERSIONS.each {version->
          // java-library plugin has three root configurations, so we apply constraints too all of
          // them so they all can use the managed versions.
          api version
          compileOnly version
          runtimeOnly version
        }
      }
      implementation platform('io.projectreactor:reactor-bom:2020.0.+')
    }
  }
}
